plugins {
    id 'idea'
    id 'jacoco'
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.16.0'
    id 'com.github.spotbugs' version '5.0.13'
    id 'org.sonarqube' version '4.0.0.2929'
    id 'com.google.cloud.artifactregistry.gradle-plugin' version '2.1.5'
}

group = gradle.projectGroup

project.ext {
    isCiServer = System.getenv().containsKey("CI")
}

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    google()
    maven {
        url 'https://us-central1-maven.pkg.dev/dsp-artifact-registry/libs-snapshot/'
    }
}

dependencies {
    // JSON processing
    ext {
        jackson = '2.13.4'
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jackson}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "${jackson}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jackson}"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'

    // Misc. Services
    testImplementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '3.0.8'

    // Google dependencies
    constraints {
        api group: 'com.google.protobuf', name: 'protobuf-java', version: '3.21.7'
        api group: 'com.google.protobuf', name: 'protobuf-java-util', version: '3.21.7'
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre' // "-jre' for Java 8 or higher
        testFixturesImplementation group: 'com.google.guava', name: 'guava', version: '31.1-jre'
        // "-jre" for Java 8 or higher
    }
    implementation platform('com.google.cloud:libraries-bom:26.68.0') // use common bom

    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub'
    implementation group: 'com.fatboyindustrial.gson-javatime-serialisers', name: 'gson-javatime-serialisers', version: '1.1.2'
    api platform('com.google.cloud:libraries-bom:26.68.0')
    api group: 'com.google.cloud', name: 'google-cloud-billing'
    api group: 'com.google.cloud', name: 'google-cloud-core-http'
    api group: 'com.google.cloud', name: 'google-cloud-storage'
    api group: 'com.google.apis', name: 'google-api-services-bigquery', version: 'v2-rev20231008-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-cloudresourcemanager', version: 'v3-rev20250606-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-compute', version: 'v1-rev20231031-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-dataproc', version: 'v1-rev20231028-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-dns', version: 'v1-rev20230831-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-iam', version: 'v1-rev20230914-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-notebooks', version: 'v1-rev20231019-2.0.0'
    api group: 'com.google.apis', name: 'google-api-services-serviceusage', version: 'v1beta1-rev20230309-2.0.0'
    api group: 'com.google.auth', name: 'google-auth-library-oauth2-http'
    api group: 'com.google.guava', name: 'guava'
    testFixturesImplementation platform('com.google.cloud:libraries-bom:26.68.0')
    testImplementation group: 'com.google.cloud', name: 'google-cloud-resourcemanager'
    testImplementation group: 'com.google.apis', name: 'google-api-services-cloudresourcemanager', version: 'v3-rev20250606-2.0.0'

    ext {
        autoValue = '1.9'
    }
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: autoValue
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: autoValue

    // Azure dependencies
    ext {
        azureCore = '1.44.1'
        azureRsrcMg = '2.39.0'
        azureRsrcMgBatch = '1.0.0'
        azureRsrcMgPostgres = '1.0.0'
    }
    api group: 'com.azure', name: 'azure-core', version: azureCore
    testFixturesImplementation group: 'com.azure', name: 'azure-core', version: azureCore
    api group: 'com.azure', name: 'azure-identity', version: '1.15.4'
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-compute', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-containerservice', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-msi', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-relay', version: '1.0.0-beta.1'
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-resources', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-batch', version: azureRsrcMgBatch
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-postgresqlflexibleserver', version: azureRsrcMgPostgres

    testFixturesImplementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-resources', version: azureRsrcMg

    // AWS dependencies
    implementation platform('software.amazon.awssdk:bom:2.20.115')
    implementation 'software.amazon.awssdk:auth'
    implementation 'software.amazon.awssdk:ec2'
    implementation 'software.amazon.awssdk:regions'
    implementation 'software.amazon.awssdk:s3'
    implementation 'software.amazon.awssdk:sagemaker'
    implementation 'software.amazon.awssdk:sts'

    // Terra libraries
    ext {
        janitorclient = '0.113.31-SNAPSHOT'
    }
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: janitorclient
    testFixturesImplementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: janitorclient

    // Logging
    ext {
        slf4j = '2.0.3'
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4j

    // Metrics & Tracing
    ext {
        openTelemetryVersion = '1.49.0'
    }
    implementation "io.opentelemetry:opentelemetry-api:${openTelemetryVersion}"
    testImplementation "io.opentelemetry:opentelemetry-sdk:${openTelemetryVersion}"
    implementation "io.opentelemetry:opentelemetry-sdk-metrics:${openTelemetryVersion}"
    implementation "io.opentelemetry.semconv:opentelemetry-semconv:1.32.0"

    // Testing
    ext {
        jUnit = '5.9.0'
    }
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jUnit
    testFixturesImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnit
    testFixturesImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    // Allow mocking final classes
    testImplementation group: 'org.mockito', name: 'mockito-inline', version: '5.2.0'
    testFixturesImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    testImplementation 'org.awaitility:awaitility:4.2.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for Gradle Build Scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

def gradleIncDir = "$rootDir/gradle"
apply from: "$gradleIncDir/dependency-locking.gradle"
apply from: "$gradleIncDir/jacoco.gradle"
apply from: "$gradleIncDir/javadoc.gradle"
apply from: "$gradleIncDir/publishing.gradle"
apply from: "$gradleIncDir/sonarqube.gradle"
apply from: "$gradleIncDir/spotbugs.gradle"
apply from: "$gradleIncDir/spotless.gradle"
apply from: "$gradleIncDir/testing.gradle"

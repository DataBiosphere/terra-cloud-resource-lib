buildscript {
    // This is all for the artifactory plugin
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.jfrog.artifactory', name: 'com.jfrog.artifactory.gradle.plugin', version: '4.13.0', ext: 'pom'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'com.diffplug.gradle.spotless' version '3.27.2'
}

group 'bio.terra'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

def artifactory_username = System.getenv('ARTIFACTORY_USERNAME')
def artifactory_password = System.getenv('ARTIFACTORY_PASSWORD')

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(artifactoryPublish) &&
            (artifactory_username == null || artifactory_password == null)) {
        throw new GradleException("Set env vars ARTIFACTORY_USERNAME and ARTIFACTORY_PASSWORD to publish")
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    compile 'com.google.auth:google-auth-library-oauth2-http:0.20.0'

    compile 'org.slf4j:slf4j-api:1.7.28'
    compile 'org.slf4j:slf4j-nop:1.7.25'

    compile 'io.opencensus:opencensus-api:0.26.0'
    runtime 'io.opencensus:opencensus-impl:0.26.0'

    compile 'com.google.cloud:google-cloud-resourcemanager:0.117.2-alpha'
    compile 'com.google.cloud:google-cloud-bigquery:1.116.0'
    compile 'com.google.cloud:google-cloud-storage:1.108.0'

    implementation 'com.google.code.gson:gson:2.8.6'
    dependencies { testCompile "org.mockito:mockito-core:2.+" }

    implementation("com.google.guava:guava:29.0-jre")
    testImplementation 'junit:junit:4.13'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testCompile 'org.apache.httpcomponents.client5:httpclient5:5.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    testCompile group: 'com.typesafe', name: 'config', version: '1.4.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform {
        includeTags 'unit'
    }
}

def integrationTest = tasks.register("integrationTest", Test) {
    useJUnitPlatform {
        includeTags "integration"
    }
    shouldRunAfter test
}

java {
    withJavadocJar()
}

publishing {
    publications {
        terraCloudResourceLibrary(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute/'
        repository {
            repoKey = 'libs-snapshot-local' // The Artifactory repository key to publish to
            username = "${artifactory_username}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('terraCloudResourceLibrary')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            // properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

// Linter
spotless {
    java {
        googleJavaFormat()
    }
}

task minniekenny(type: Exec) {
    commandLine 'sh', './minnie-kenny.sh'
}

test.dependsOn minniekenny

// Suppress gradle javadoc errors and warnings
tasks.withType(Javadoc) {
    failOnError false
    options.addStringOption('Xdoclint:none', '-quiet')
    options.addStringOption('encoding', 'UTF-8')
    options.addStringOption('charSet', 'UTF-8')
}

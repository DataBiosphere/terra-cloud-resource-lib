buildscript {
    // This is all for the artifactory plugin
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.jfrog.artifactory', name: 'com.jfrog.artifactory.gradle.plugin', version: '4.13.0', ext: 'pom'
    }
}

plugins {
    id 'java-library'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'com.diffplug.gradle.spotless' version '3.27.2'
}

group 'bio.terra'
version '0.0.1-SNAPSHOT'

sourceCompatibility = 1.8

def artifactory_username = System.getenv('ARTIFACTORY_USERNAME')
def artifactory_password = System.getenv('ARTIFACTORY_PASSWORD')

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(artifactoryPublish) &&
            (artifactory_username == null || artifactory_password == null)) {
        throw new GradleException("Set env vars ARTIFACTORY_USERNAME and ARTIFACTORY_PASSWORD to publish")
    }
}

repositories {
    mavenCentral()
    google()
}

dependencies {
    // this is just a random dependency added to make sure I am doing it right, remove when we have a real one
    compile group: 'com.uber.cadence', name: 'cadence-client', version: '2.7.1'
    compile group: 'commons-configuration', name: 'commons-configuration', version: '1.9'
    compile group: 'ch.qos.logback', name: 'logback-classic', version: '1.2.3'

    compile 'com.google.cloud:google-cloud-storage:1.106.0'
    compile 'com.google.auth:google-auth-library-oauth2-http:0.20.0'
    compile 'com.google.apis:google-api-services-cloudbilling:v1-rev48-1.25.0'
    compile 'com.google.apis:google-api-services-serviceusage:v1beta1-rev255-1.25.0'
    compile 'com.google.apis:google-api-services-cloudresourcemanager:v1-rev563-1.25.0'

    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
    testImplementation 'org.hamcrest:hamcrest:2.2'
    testCompile 'org.apache.httpcomponents.client5:httpclient5:5.0'
    testCompile group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.9.8'
    testCompile group: 'com.typesafe', name: 'config', version: '1.4.0'

    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'
}

test {
    useJUnitPlatform {
        includeTags 'unit', 'integration'
    }
}

java {
    withJavadocJar()
}

publishing {
    publications {
        terraCloudResourceLibrary(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute/'
        repository {
            repoKey = 'libs-snapshot-local' // The Artifactory repository key to publish to
            username = "${artifactory_username}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('terraCloudResourceLibrary')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            // properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

// Linter
spotless {
    java {
        googleJavaFormat()
    }
}

task minniekenny(type: Exec) {
    commandLine 'sh', './minnie-kenny.sh'
}

test.dependsOn minniekenny

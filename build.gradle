plugins {
    id 'idea'
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'

    id 'com.diffplug.spotless' version '6.10.0'
    id 'com.jfrog.artifactory' version '4.29.0'
}

group = 'bio.terra'

// If true, search local repository (~/.m2/repository/) first for dependencies.
def useMavenLocal = false
repositories {
    if (useMavenLocal) {
        mavenLocal() // must be listed first to take effect
    }
    mavenCentral()
    google()
    maven {
        url 'https://broadinstitute.jfrog.io/broadinstitute/libs-snapshot-local/'
    }
}

dependencies {
    // JSON processing
    ext {
        jackson = '2.13.4'
    }
    implementation group: 'com.fasterxml.jackson.core', name: 'jackson-core', version: "${jackson}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jdk8', version: "${jackson}"
    implementation group: 'com.fasterxml.jackson.datatype', name: 'jackson-datatype-jsr310', version: "${jackson}"
    implementation group: 'com.google.code.gson', name: 'gson', version: '2.9.1'

    // Misc. Services
    testImplementation group: 'org.glassfish.jersey.core', name: 'jersey-client', version: '3.0.8'

    // Google dependencies
    constraints {
        implementation group: 'com.google.guava', name: 'guava', version: '31.1-jre' // "-jre' for Java 8 or higher
        testFixturesImplementation group: 'com.google.guava', name: 'guava', version: '31.1-jre' // "-jre" for Java 8 or higher
    }
    implementation platform('com.google.cloud:libraries-bom:26.0.0') // use common bom
    implementation group: 'com.google.cloud', name: 'google-cloud-pubsub'
    api group: 'com.google.apis', name: 'google-api-services-bigquery', version: 'v2-rev20220716-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-cloudresourcemanager', version: 'v3-rev20220710-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-compute', version: 'v1-rev20220720-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-dns', version: 'v2-rev20220505-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-iam', version: 'v1-rev20220526-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-notebooks', version: 'v1-rev20220716-1.32.1'
    api group: 'com.google.apis', name: 'google-api-services-serviceusage', version: 'v1beta1-rev20220615-1.32.1'
    api group: 'com.google.auth', name: 'google-auth-library-oauth2-http'
    api group: 'com.google.cloud', name: 'google-cloud-billing'
    api group: 'com.google.cloud', name: 'google-cloud-core-http'
    api group: 'com.google.cloud', name: 'google-cloud-storage'
    api group: 'com.google.guava', name: 'guava'
    testFixturesImplementation platform('com.google.cloud:libraries-bom:26.0.0')
    testImplementation group: 'com.google.cloud', name: 'google-cloud-resourcemanager'

    ext {
        autoValue = '1.9'
    }
    implementation group: 'com.google.auto.value', name: 'auto-value-annotations', version: autoValue
    annotationProcessor group: 'com.google.auto.value', name: 'auto-value', version: autoValue

    // Azure dependencies
    ext {
        azureCore = '1.32.0'
        azureRsrcMg = '2.19.0'
    }
    api group: 'com.azure', name: 'azure-core', version: azureCore
    testFixturesImplementation group: 'com.azure', name: 'azure-core', version: azureCore
    api group: 'com.azure', name: 'azure-identity', version: '1.6.0'
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-compute', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-containerinstance', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-msi', version: azureRsrcMg
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-relay', version: '1.0.0-beta.1'
    api group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-resources', version: azureRsrcMg
    testFixturesImplementation group: 'com.azure.resourcemanager', name: 'azure-resourcemanager-resources', version: azureRsrcMg

    // Terra libraries
    ext {
        janitorclient = '0.114.0-SNAPSHOT'
    }
    implementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: janitorclient
    testFixturesImplementation group: 'bio.terra', name: 'terra-resource-janitor-client', version: janitorclient

    // Logging
    ext {
        slf4j = '2.0.3'
    }
    implementation group: 'org.slf4j', name: 'slf4j-api', version: slf4j
    testImplementation group: 'org.slf4j', name: 'slf4j-simple', version: slf4j

    // Metrics & Tracing
    ext {
        opencensus = '0.31.1'
    }
    implementation group: 'io.opencensus', name: 'opencensus-api', version: opencensus
    implementation group: 'io.opencensus', name: 'opencensus-impl', version: opencensus

    // Testing
    ext {
        jUnit = '5.9.0'
    }
    testImplementation group: 'org.junit.jupiter', name: 'junit-jupiter', version: jUnit
    testFixturesImplementation group: 'org.junit.jupiter', name: 'junit-jupiter-api', version: jUnit
    testRuntimeOnly group: 'org.junit.jupiter', name: 'junit-jupiter-engine', version: jUnit
    testFixturesImplementation group: 'junit', name: 'junit', version: '4.13.2'
    testImplementation group: 'org.hamcrest', name: 'hamcrest', version: '2.2'
    testImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
    testFixturesImplementation group: 'org.mockito', name: 'mockito-core', version: '4.8.0'
}

java {
    toolchain {
        languageVersion.set(JavaLanguageVersion.of(17))
    }
}

// for Gradle Build Scans
if (hasProperty('buildScan')) {
    buildScan {
        termsOfServiceUrl = 'https://gradle.com/terms-of-service'
        termsOfServiceAgree = 'yes'
    }
}

apply from: "$rootDir/gradle/dependency-locking.gradle"
apply from: "$rootDir/gradle/javadoc.gradle"
apply from: "$rootDir/gradle/publishing.gradle"
apply from: "$rootDir/gradle/spotless.gradle"
apply from: "$rootDir/gradle/test.gradle"

buildscript {
    // This is all for the artifactory plugin
    repositories {
        jcenter()
    }
    dependencies {
        classpath group: 'com.jfrog.artifactory', name: 'com.jfrog.artifactory.gradle.plugin', version: '4.13.0', ext: 'pom'
    }
}

plugins {
    id 'java-library'
    id 'java-test-fixtures'
    id 'maven-publish'
    id 'com.jfrog.artifactory' version '4.13.0'
    id 'com.diffplug.gradle.spotless' version '3.27.2'
}

group 'bio.terra'
version '0.0.1-SNAPSHOT'


def artifactory_username = System.getenv('ARTIFACTORY_USERNAME')
def artifactory_password = System.getenv('ARTIFACTORY_PASSWORD')

gradle.taskGraph.whenReady { taskGraph ->
    if (taskGraph.hasTask(artifactoryPublish) &&
            (artifactory_username == null || artifactory_password == null)) {
        throw new GradleException("Set env vars ARTIFACTORY_USERNAME and ARTIFACTORY_PASSWORD to publish")
    }
}

subprojects {
    repositories {
        mavenCentral()
        google()
    }

    apply plugin: 'java-library'
    apply plugin: 'java-test-fixtures'
    apply plugin: 'com.diffplug.gradle.spotless'

    dependencies {
        api 'com.google.auth:google-auth-library-oauth2-http:0.20.0'
        api 'com.google.cloud:google-cloud-core-http:1.93.5'

        implementation 'org.slf4j:slf4j-api:1.7.28'

        implementation 'com.google.code.gson:gson:2.8.6'
        testImplementation "org.mockito:mockito-core:2.+"

        implementation("com.google.guava:guava:29.0-jre")
        testImplementation 'junit:junit:4.13'
        testFixturesApi 'junit:junit:4.13'
        testImplementation 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testFixturesApi 'org.junit.jupiter:junit-jupiter-api:5.6.2'
        testImplementation 'org.hamcrest:hamcrest:2.2'
        testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.6.2'

        testImplementation(testFixtures(project(':common')))
    }

    processTestResources {
        // Include common/testFixtures/resources for integration test credentials.
        from("${project(':common:').projectDir}/testFixtures/resources")
    }

    // Linter
    spotless {
        java {
            googleJavaFormat()
        }
    }

    test {
        useJUnitPlatform {
            includeTags 'unit'
        }
    }

    def integrationTest = tasks.register("integrationTest", Test) {
        useJUnitPlatform {
            includeTags "integration"
        }
        shouldRunAfter test
    }

    java {
        sourceCompatibility = JavaVersion.VERSION_1_8
        withJavadocJar()
    }

    task minniekenny(type: Exec) {
        commandLine 'sh', '../minnie-kenny.sh'
    }

    test.dependsOn minniekenny
}

// TODO(CA-825) Publish packages from subprojects separately.
publishing {
    publications {
        terraCloudResourceLibrary(MavenPublication) {
            from components.java
        }
    }
}

artifactory {
    publish {
        contextUrl = 'https://broadinstitute.jfrog.io/broadinstitute/'
        repository {
            repoKey = 'libs-snapshot-local' // The Artifactory repository key to publish to
            username = "${artifactory_username}" // The publisher user name
            password = "${artifactory_password}" // The publisher password
        }
        defaults {
            // Reference to Gradle publications defined in the build script.
            // This is how we tell the Artifactory Plugin which artifacts should be
            // published to Artifactory.
            publications('terraCloudResourceLibrary')
            publishArtifacts = true
            // Properties to be attached to the published artifacts.
            // properties = ['qa.level': 'basic', 'dev.team' : 'core']
            // Publish generated POM files to Artifactory (true by default)
            publishPom = true
        }
    }
}

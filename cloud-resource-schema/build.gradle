apply plugin: 'org.openapi.generator'

dependencies {
    // -- OpenAPI CodeGen dependencies --
    api group: 'com.fasterxml.jackson.core', name: 'jackson-annotations', version: '2.11.0'
    api group: 'com.fasterxml.jackson.core', name: 'jackson-databind', version: '2.11.0'
    // TODO: this version of swagger-annotations is old, but the code gen is still relying on it
    //  There is an open bug tracking the fix: https://github.com/OpenAPITools/openapi-generator/issues/4245
    api group: 'io.swagger', name: 'swagger-annotations', version: '1.6.0'
}

def openapiSourceFile = "$projectDir/src/main/resources/cloud_resources_uid.yaml"
def openapiTargetFolder = "$buildDir/generated/openapi".toString()

openApiGenerate {
    generatorName = 'java'
    inputSpec = openapiSourceFile
    outputDir = openapiTargetFolder
    library = 'jersey2'
    modelPackage = 'bio.terra.cloudres.resources'
    configOptions = [
            'dateLibrary': 'java8'
    ]
    systemProperties = [
            // Only generate model java classes and not API classes.
            models   : '',
            modelDocs : 'false',
            modelTests: 'false',
    ]
}

compileJava.dependsOn tasks.openApiGenerate
ideaModule.dependsOn tasks.openApiGenerate

sourceSets.main.java.srcDir("$openapiTargetFolder/src/main/java")
